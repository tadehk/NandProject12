// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Keyboard.jack

/**
 * A library for handling user input from the keyboard.
 */
class Keyboard {
    
    static Array keyboard;

    function void init() {
        let keyboard = 24576;
        return;
    } 
	
    function char keyPressed() {
        return keyboard[0];
    }

    function char readChar() {
        var char key;
        while(Keyboard.keyPressed() = 0){}
        let key = Keyboard.keyPressed();
        while(~(Keyboard.keyPressed() = 0)){}
        do Output.printChar(key);
        return key;
    }

    function String readLine(String message) {
        var String line;
        var char c;

        do Output.printString(message);

        let line = String.new(50);

        let c = Keyboard.readChar();
        while(~(c = String.newLine())){
            if(c = String.backSpace()){
                do line.eraseLastChar();
            }else{
                do line.appendChar(c);
            }
            let c = Keyboard.readChar();
        }

        return line;
    }   

    function int readInt(String message) {
        var String line;
        
        let line = Keyboard.readLine(message);
        return line.intValue();
    }
}